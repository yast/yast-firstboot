/* ------------------------------------------------------------------------------
 * Copyright (c) 2012 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	firstboot/src/inst_firstboot_finish.ycp
 * Module:	Installation
 * Summary:	Finish Firstboot
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * Display a nice congratulation message for the user.
 *
 * $Id$
 */

{
    textdomain "firstboot";

    import "FileUtils";
    import "Misc";
    import "Mode";
    import "Wizard";
    import "Popup";
    import "Label";
    import "Firstboot";
    import "GetInstArgs";

    map display = UI::GetDisplayInfo();

    if (!display["TextMode"]:true)
    {
	if (!Pkg::IsProvided ("yast2-control-center"))
	{
            Firstboot::show_y2cc_checkbox = false;
	}
    }

    integer space = display["TextMode"]:true ? 1 : 3;

    // Check box: Should the YaST2 control center automatically
    // be started after this part of the installation is done?
    // Translators: About 40 characters max,
    // use newlines for longer translations.
    term check_box_start_y2cc = `Empty();
    if (Firstboot::show_y2cc_checkbox)
    {
        check_box_start_y2cc = `CheckBox (`id(`start_y2cc),
					   _("&Start YaST Control Center"), false);
    }

    // caption for dialog "Congratulation Dialog"
    string caption = _("Configuration Completed");

    // congratulation text 1/4
    string text = _("<p><b>Congratulations!</b></p>") +

    // congratulation text 2/4
	_("<p>The installation of &product; on your machine is complete.
After clicking <b>Finish</b>, you can log in to the system.</p>
") +

    // congratulation text 3/4
    // Translators: If there exists a SuSE web-page for your language
    // change the address accordingly. If in doubt leave the original.
	_("<p>Visit us at www.suse.com.</p>") +

    // congratulation text 4/4
	_("<p>Have a lot of fun!<br>Your SuSE Development Team</p>");


    // If text exists, read it from file instead; it is expected to be richtext.
    string finish_text		= "";
    string finish_text_file	= Misc::SysconfigRead (
        .sysconfig.firstboot.FIRSTBOOT_FINISH_FILE, "");
    if (finish_text_file != "" && FileUtils::Exists (finish_text_file))
    {
	finish_text	= (string)SCR::Read (.target.string, finish_text_file);
    }
    if (finish_text == nil || finish_text == "")
	finish_text	= text;

    term contents = `VBox (
	`VSpacing (space),
	`HBox (
	    `HSpacing (2*space),
	    `RichText (finish_text),
	    `HSpacing (2*space)
	),
	`VSpacing (space),
	check_box_start_y2cc,
	`VSpacing (2)
   );

    // help 1/4 for dialog "Congratulation Dialog"
    string help = _("<p>Your system is ready for use.</p>") +

    // help 2/4 for dialog "Congratulation Dialog"
	_("<p><b>Finish</b> will close the YaST installation and continue
to the login screen.</p>
") +

    // help 3/4 for dialog "Congratulation Dialog"
	_("<p>If you choose the default graphical desktop KDE, you can
adjust some KDE settings to your hardware. Also notice
our SuSE Welcome Dialog.</p>
");

    if (Firstboot::show_y2cc_checkbox) {

    // help 4/4 for dialog "Congratulation Dialog"
    help = help + _("<p>If desired, experts can use the full range of SuSE's configuration
modules at this time. Check <b>Start YaST Control Center</b> and it will start
after <b>Finish</b>. Note: The Control Center does not have a back button to
return to this installation sequence.</p>
");
    }

    Wizard::SetContents (caption, contents, help, GetInstArgs::enable_back(),
                    GetInstArgs::enable_next());

    Wizard::SetNextButton(`next, Label::FinishButton() );
    Wizard::SetFocusToNextButton();


    any ret = nil;

    repeat {

	ret = Wizard::UserInput();

	if (ret == `abort)
	{
	    if (Popup::ConfirmAbort (`incomplete))
		break;
	}
	else if ( ret == `help )
	{
	    Wizard::ShowHelp (help);
	}

    } until ( ret == `next || ret == `back );

    Wizard::RestoreNextButton ();

    if (ret == `next && (boolean)UI::QueryWidget (`id(`start_y2cc), `Value))
    {
	// Create empty /var/lib/YaST2/start_y2cc file to signal the calling script
	// that the YaST2 control center should be started after the installation
	SCR::Write (.target.string, "/var/lib/YaST2/start_y2cc", "");
    }

    return ret;
}
