/**
 * File:	firstboot/src/firstboot_desktop.ycp
 * Module:	Installation
 * Summary:	Firstboot Desktop Selection
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */
{
    textdomain "firstboot";
    import "Wizard";
    import "Firstboot";
    import "GetInstArgs";

    /**
     * Check if both selections are installed
     * @return false if not
     */
    define boolean checkSelections()
    {
        Pkg::TargetInit ("/", false);
        list<string> installed_desktop_selections = Pkg::GetSelections( `installed, "base");
        y2milestone ("available_desktop_selections %1", installed_desktop_selections);

        if (!contains (installed_desktop_selections, "Gnome"))
        {
            y2error( "No desktop selections installed for Gnome" );
            return false;
        }
        if (!contains (installed_desktop_selections, "Kde"))
        {
            y2error( "No desktop selections installed Kde" );
            return false;
        }

        Firstboot::installed_desktops = installed_desktop_selections;
        return true;
    }


    // Continue, we have only one desktop installed
    if (!checkSelections())
    {
        return `auto;
    }

    map<string, string> selection_description = $[];
    foreach( string selection, Firstboot::installed_desktops, {
	map selection_data = Pkg::SelectionData (selection);
	if (selection_data != nil)
	{
	    selection_description[selection] = selection_data["summary"]:("'"+selection+"'");
	}
    });

    // Construct a box with radiobuttons for each software base configuration
    term selection_box = `RadioButtonGroup(`id(`selection_box),
					    `VBox(
						  `Left(`RadioButton(`id("gnome"), `opt(`notify), selection_description["Gnome"]:"not defined",
                                                          Firstboot::default_wm == "gnome")),
						  `VSpacing(1),
						  `Left(`RadioButton(`id("kde"), `opt(`notify), selection_description["Kde"]:"not defined",
                                                          Firstboot::default_wm == "kde"))
						  )
					    );


    term ask_desktop_dialog = `HBox(
                                    `HStretch(),
				    `VBox (
                                        `VWeight(30,`VStretch()),
					// translators: dialog text
					`Left(`Label (_("Please select the desktop environment which you would like
to use from the list below."))),
					`VSpacing(1),
                                        `VWeight(10,`VStretch()),
					`HBox(
					      `HSpacing(10),
					       selection_box
                                         ),
                                       `VWeight(60,`VStretch())
                                       ),
                                   `HStretch()
				    );


    // help text fro desktop dialog
    string desktop_help_text = _("<h3>Desktop Selections</h3>
<p>This system has more than one desktop environment installed, select
the desktop you want to enable as the default desktop.</p>");

    // translators: dialog title
    Wizard::SetContents ( _("Select Your Default Desktop"), ask_desktop_dialog,
            desktop_help_text, GetInstArgs::enable_back(),
                    GetInstArgs::enable_next());

    any ret = nil;
    repeat
    {
        ret = UI::UserInput();
        // get the newly selected base configuration
        Firstboot::default_wm = (string) UI::QueryWidget( `id(`selection_box),`CurrentButton );

    } until (ret == `next || ret == `back || ret == `abort);

    return (symbol)ret;
//EOF
}
