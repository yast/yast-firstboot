/**
 * Maintainer:             Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */
{
    textdomain "firstboot";

    import "Mode";
    import "Stage";
    import "ProductControl";
    import "Wizard";
    import "Report";
    import "Firstboot";
    import "Misc";


    Wizard::OpenNextBackStepsDialog();

    string wizard_mode = Mode::test () ? "installation" : Mode::mode ();

    list<map> stage_mode = [$["stage": "firstboot",  "mode": wizard_mode ]];
    ProductControl::AddWizardSteps(stage_mode);

    // Do log Report messages by default (#180862)
    Report::LogMessages(true);
    Report::LogErrors(true);
    Report::LogWarnings(true);


    symbol ret = ProductControl::Run();
    y2milestone("ProductControl::Run() returned: %1", ret);

    Pkg::SourceFinishAll();
    Pkg::TargetFinish();

    UI::CloseDialog();

    // handle abort
    if (ret == `abort)
    {
	// do the same action as if the license has not been accepted
	string action = Misc::SysconfigRead(.sysconfig.firstboot.LICENSE_REFUSAL_ACTION, "halt");
	y2milestone("Firstboot aborted, LICENSE_REFUSAL_ACTION: %1", action);

	if (action == "halt")
	{
	    y2milestone("Halting the system...");
	    SCR::Execute(.target.bash, "/sbin/halt");
	}
	else if (action == "reboot")
	{
	    y2milestone("Rebooting the system...");
	    SCR::Execute(.target.bash, "/sbin/reboot");
	}
	else if (action == "continue")
	{
	    y2milestone("Finishing Yast...");
	}
	else
	{
	    y2error("Unknown action: %1", action);
	}
    }

    return ret;
}
